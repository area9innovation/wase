import wase/parse;
import tools/dsl/dsl_include;
import wase/type;
import wase/compile;
import wase/wase_parse_typed;
import wase/wase_types2dsl;
import wase/dce;
import wase/pretty_wase;
import wase/pretty_wase_ast;
import formats/wasm/wasm_encode;
import formats/json/any2json;

compileWase2File(includes, filename : string, code : string) -> Promise<int, string> {
	Promise(\onDone : (int) -> void, onError : (string) -> void -> {
		println("\nCompiling '" + filename + "'");
		program0 = parseWase(filename, code);

		// parseWeDecl(code : string, onError : (string) -> void) -> WeDecl;
		// waseAst2DslAst(w : WaseAst) -> DslAst;

		prog1 = parseWeDecl(getFileContent(filename), \err -> println("Error: " + err));

		program1 = waseAst2DslAst(prog1);

		/*if (program != program1 && isUrlParameterTrue("test-generated-ast")) {
			println("programms differ:\n" +
				prettyWaseAst(prog1) + "\n" +
				"\n------- PROGRAM -------------\n" + 
				//json2stringFormatted(any2json(program)) + 
				prettyDsl(program) +
				"\n------- PROGRAM 1 ------------\n" + 
				//json2stringFormatted(any2json(program1)) + 
				prettyDsl(program1) +
				"\n--------------------\n"
			);
			fail("FAIL");
		}*/

		program = if (isUrlParameterTrue("test-generated-ast")) program1 else program0;

		include = DslInclude(includes, [".wase"], "wase_include", "wase_empty", \path, file, content -> {
			parseWase(path, content);
		}, ref makeSet());
		extended = processDslIncludes(include, program);
		typed = typeWase(extended);

		if (isUrlParameterTrue("debug-type")) {
			println("Typed '" + filename + "'':");
			println(prettyDsl(typed));
		}

		wase0 = dsl2wase(\node, error -> {
			println(filename + ": " + error)
		}, typed);

		// Dead code optimization
		dce = getUrlParameterDef("dce", "1");
		wase = if (dce == "0") wase0 else performDCE(wase0, dce);

		if (isUrlParameterTrue("debug-wase")) {
			println("\nAs wase:");
			println(prettyWase(wase));
		}

		wasm = wase2wasm(wase);
		bytes = wasmModule2bytes(wasm);

		filebase = changeFileExt(filename, "");
		ok = setFileContentBytes(filebase + ".wasm", 
			fold(bytes, "", \acc, byte -> {
				acc + fromCharCode(byte)
			})
		);
		if (ok) {
			println("  '" + filebase + ".wasm' saved");
			if (isUrlParameterTrue("wat")) {
				args = concat([
						filebase + ".wasm", "-o", filebase + ".wat",
						//"--enable-all", 
					], if (isUrlParameterTrue("no-check")) ["--no-check"] else [],
				);
				startProcess("wasm2wat", args, ".", "", \ec : int, so, se -> {
					if (ec == 0) {
						println("  '" + filebase + ".wat' generated");
						onDone(ec);
					} else {
						onError(filename + ":\n" + so + se);
					}
				});
			} else onDone(0);
		} else {
			onError("Could not save " + filebase + ".wasm saved")
		}
	});
}

main() {
	includes = strSplit(getUrlParameter("I"), ",");
	files0 : [string] = filtermap(getAllUrlParametersArray(), \a -> if (a[1] == "") Some(changeFileExt(a[0], ".wase")) else None());
	files = if (files0 != []) files0 else {
		if (isUrlParameterTrue("test")) {
			test = getUrlParameter("test");
			testFolder = if (test == "1") "tests" else test;
			tests = readDirectory(testFolder);
			filtermap(tests, \t -> if (endsWith(t, ".wase")) Some(pathCombine(testFolder, t)) else None());
		} else [];
	}
	if (files == []) {
		printWaseHelp();
		quit(1);
	} else {
		compiles : [Promise<int, string>]= map(files, \file0 -> {
			file = resolveDslFile(includes, file0);
			if (fileExists(file)) {
				compileWase2File(includes, file, getFileContent(file))
			} else {
				Promise(\__, onError -> onError(file0 + " not found"));
			}
		});
		all : Promise<[int], string> = allP(compiles);
		doneP(all, \__ -> quit(0), \error -> {
			println(error);
			quit(1);
		});
	}
}

printWaseHelp() -> void {
	println("wase <files> - Compiles the files to <file>.wasm files");
	println("  I=<path>,...   Defines the include path for resolving .wase files and includes");
	println("");
	println("  wat=1          Produce .wat disassembly using wasm2wat");
	println("  nocheck=1      Pass --nocheck to wasm2wat when producing .wat for invalid Wasm");
//	println("  run=wasm3        Runs the .wasm using wasm3");
//	println("  run=wasmer       Runs the .wasm using wasmer");
	println("");
	println("  verbose=1      Print how includes are resolved to files");
	println("");
	println("  test=1         Compiles all test cases in the tests/ folder");
	println("  debug-type=1   Output the resulting typed AST");
	println("  debug-wase=1   Output the internal intermediate WASE representation");
	println("  dce=1          Perform a dead code optimization (if on by default)");
	println("  dce=0          Skip a dead code optimization");
	println("  dce=id1,id2,.. Perform a dead code optimization with a list of given ids preserved");
	println("  dce-verbose=1  Show dead toplevels");
}
