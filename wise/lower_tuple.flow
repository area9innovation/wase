import wise/type_ast;
import wise/wiseast_utils;
import wise/wise_template_fn;

export {
	// tuple tree -> flat tuple
	lowerTuplesInWiseAst(
		ast : WiseAst,
		unionFindMap : UnionFindMap<[WiseAstType]>,
		tmpCntr : ref int,
		onError : (WiseAst, string) -> void,
	) -> WiseAst;
}

lowerTuplesInWiseAst(ast : WiseAst, unionFindMap : UnionFindMap<[WiseAstType]>, tmpCntr : ref int, onError : (WiseAst, string) -> void) -> WiseAst {
	rec = \v -> lowerTuplesInWiseAst(v, unionFindMap, tmpCntr, onError);

	switch (ast : WiseAst) {
		WiseAstList(value): WiseAstList(mapList(value, rec));
		WiseAstBool(value): ast;
		WiseAstInt(value): ast;
		WiseAstDouble(value): ast;
		WiseAstString(value): ast;
		WiType() : lowerTuplesInWiType(ast);
		WiIdtype(id, t, eclass): WiIdtype(id, lowerTuplesInWiType(t), eclass);
		WiDecl() : lowerTuplesInWiDecl(ast, unionFindMap, tmpCntr, onError);
		WiExp() : lowerTuplesInWiExp(ast, unionFindMap, tmpCntr, 0, onError);
	}
}

lowerTuplesInWiDecl(v : WiDecl, unionFindMap : UnionFindMap<[WiseAstType]>, tmpCntr : ref int, onError : (WiseAst, string) -> void) -> WiDecl {
	switch (v : WiDecl) {
		WiFunction(optexport, id, templates, lambdaargs, type, expr, decl, eclass): WiFunction(
			optexport,
			id,
			templates,
			map(lambdaargs, \a -> WiIdtype(a.id, lowerTuplesInWiType(a.type), a.eclass)),
			lowerTuplesInWiType(type),
			lowerTuplesInWiExp(expr, unionFindMap, tmpCntr, 0, onError),
			lowerTuplesInWiDecl(decl, unionFindMap, tmpCntr, onError),
			eclass
		);
		WiGlobal(id, optexport, type, optmutable, expr, decl, eclass): WiGlobal(
			id,
			optexport,
			lowerTuplesInWiType(type),
			optmutable,
			lowerTuplesInWiExp(expr, unionFindMap, tmpCntr, 0, onError),
			lowerTuplesInWiDecl(decl, unionFindMap, tmpCntr, onError),
			eclass
		);
		WiImport(id1, type, optmutable, id2, id3, decl, eclass): WiImport(
			id1,
			lowerTuplesInWiType(type),
			optmutable,
			id2,
			id3,
			lowerTuplesInWiDecl(decl, unionFindMap, tmpCntr, onError),
			eclass
		);
		WiWiseInclude(s, decl, eclass): WiWiseInclude(s, lowerTuplesInWiDecl(decl, unionFindMap, tmpCntr, onError), eclass);
		WiWaseEmpty(__): v;

	}
}

lowerTuplesInWiType(v : WiType) -> WiType {
	switch (v : WiType) {
		WiBasictype(__, __) : v;
		WiFntype(types, type0, eclass): WiFntype(map(types, lowerTuplesInWiType), lowerTuplesInWiType(type0), eclass);
		WiTupletype(types, eclass) : WiTupletype(unrollWiTupletype(types), 0);
	}
}

unrollWiTupletype(types : [WiType]) -> [WiType] {
	mapConcat(types, \t -> switch (t : WiType) {
		WiBasictype(__, __) : [t];
		WiFntype(ttypes, type0, eclass): {
			retTypes = unrollWiTupletype([type0]);
			[WiFntype(unrollWiTupletype(ttypes), if (retTypes == []) type0 else retTypes[0], eclass)];
		}
		WiTupletype(ttypes, eclass) : unrollWiTupletype(ttypes);
	});
}

lowerTuplesInWiExp(v : WiExp, unionFindMap : UnionFindMap<[WiseAstType]>, tmpCntr : ref int, resEclass : int, onError : (WiseAst, string) -> void) -> WiExp {
	rec = \v2 -> lowerTuplesInWiExp(v2, unionFindMap, tmpCntr, 0, onError);
	recWithType = \v2, knownEclass -> lowerTuplesInWiExp(v2, unionFindMap, tmpCntr, knownEclass, onError);
	lowerTuplesInWiBrace = \v2 -> WiBrace(map(v2.expsemi, rec), v2.eclass);

	switch (v : WiExp) {
		WiAdd(exp1, exp2, eclass): WiAdd(rec(exp1), rec(exp2), eclass);
		WiAnd(exp1, exp2, eclass): WiAnd(rec(exp1), rec(exp2), eclass);
		WiBool(b, __): v;
		WiBreak(__): v;
		WiContinue(__): v;
		WiDouble(__, __): v;
		WiHex32(s, __): v;
		WiHex64(s, __): v;
		WiHexf32(s, __): v;
		WiHexf64(s, __): v;
		WiI64(s, __): v;
		WiIf(exp1, exp2, eclass): WiIf(rec(exp1), rec(exp2), eclass);
		WiIfelse(exp1, exp2, exp3, eclass): WiIfelse(rec(exp1), rec(exp2), rec(exp3), eclass);
		WiReturn(exps, eclass): WiReturn(map(exps, rec), eclass);
		WiSet(id, e, eclass): WiSet(id, rec(e), eclass);
		WiTuple(exps, eclass): unrollWiTuple(v, unionFindMap, tmpCntr, onError);
		WiWhile(exp1, exp2, eclass): WiWhile(rec(exp1), rec(exp2), eclass);
		WiBoolAnd(exp1, exp2, eclass): WiBoolAnd(rec(exp1), rec(exp2), eclass);
		WiBoolNot(e, eclass): WiBoolNot(rec(e), eclass);
		WiBoolOr(exp1, exp2, eclass): WiBoolOr(rec(exp1), rec(exp2), eclass);
		WiBrace(expsemi, eclass): lowerTuplesInWiBrace(v);
		WiCall(e, exps, eclass): WiCall(rec(e), map(exps, rec), eclass);
		WiDiv(exp1, exp2, eclass): WiDiv(rec(exp1), rec(exp2), eclass);
		WiEqual(exp1, exp2, eclass): WiEqual(rec(exp1), rec(exp2), eclass);
		WiGreater(exp1, exp2, eclass): WiGreater(rec(exp1), rec(exp2), eclass);
		WiGreaterEqual(exp1, exp2, eclass): WiGreaterEqual(rec(exp1), rec(exp2), eclass);
		WiGreaterEqualu(exp1, exp2, eclass): WiGreaterEqualu(rec(exp1), rec(exp2), eclass);
		WiGreateru(exp1, exp2, eclass): WiGreateru(rec(exp1), rec(exp2), eclass);
		WiIsNull(e, eclass): WiIsNull(rec(e), eclass);
		WiLess(exp1, exp2, eclass): WiLess(rec(exp1), rec(exp2), eclass);
		WiLessEqual(exp1, exp2, eclass): WiLessEqual(rec(exp1), rec(exp2), eclass);
		WiLessEqualu(exp1, exp2, eclass): WiLessEqualu(rec(exp1), rec(exp2), eclass);
		WiLessu(exp1, exp2, eclass): WiLessu(rec(exp1), rec(exp2), eclass);
		WiLet(id, i, opttype, e, brace, eclass): WiLet(id, i, lowerTuplesInWiType(opttype), recWithType(e, opttype.eclass), lowerTuplesInWiBrace(brace), eclass);
		WiMod(exp1, exp2, eclass): WiMod(rec(exp1), rec(exp2), eclass);
		WiMul(exp1, exp2, eclass): WiMul(rec(exp1), rec(exp2), eclass);
		WiNegate(e, eclass): WiNegate(rec(e), eclass);
		WiNotEqual(exp1, exp2, eclass): WiNotEqual(rec(exp1), rec(exp2), eclass);
		WiOr(exp1, exp2, eclass): WiOr(rec(exp1), rec(exp2), eclass);
		WiSub(exp1, exp2, eclass): WiSub(rec(exp1), rec(exp2), eclass);
		WiTypeannotation(e, type, eclass): WiTypeannotation(rec(e), lowerTuplesInWiType(type), eclass);
		WiWasmOp(s, wargs, exps, eclass): lowerTuplesInWiWasmOp(v, unionFindMap, tmpCntr, resEclass, onError);
		WiXor(exp1, exp2, eclass): WiXor(rec(exp1), rec(exp2), eclass);
		WiSizeoftype(type, eclass): WiSizeoftype(lowerTuplesInWiType(type), eclass);
		WiPolycall(e, types, eclass): WiPolycall(rec(e), map(types, lowerTuplesInWiType), eclass);
		WiTupledot(e, ind, eclass): lowerWiTupledot(unionFindMap, v, tmpCntr, onError);
		WiDefoftype(type, eclass): WiDefoftype(lowerTuplesInWiType(type), eclass);
		WiInt(int_0, eclass): v;
		WiVar(id, eclass): lowerTuplesInWiVar(v, unionFindMap, onError);
	}
}

lowerTuplesInWiVar(v : WiExp, unionFindMap : UnionFindMap<[WiseAstType]>, onError :(WiseAst, string) -> void) -> WiExp {
	fieldType = resolveWiseAstTypeByEclass(unionFindMap, v.eclass, onError);
	switch (fieldType : WiseAstType) {
		default : v;
		WiseAstTypeTuple(__) : {
			ttype = lowerTuplesInWiType(wiseAstType2wiType(fieldType, v.eclass, unionFindMap));
			switch (ttype : WiType) {
				WiTupletype(ttypes, eclass2): WiTuple(mapi(ttypes, \j, t -> WiTupledot(v, j, t.eclass)), eclass2);
				default : v;
			}
		}
		
	}
}

lowerWiTupledot(unionFindMap : UnionFindMap<[WiseAstType]>, v : WiTupledot, tmpCntr : ref int, onError : (WiseAst, string) -> void) -> WiTupledot {
	value = extractWiTupledotValue(v);
	loweredValue = lowerTuplesInWiExp(value, unionFindMap, tmpCntr, 0, onError);
	fieldType = resolveWiseAstTypeByEclass(unionFindMap, value.eclass, onError);
	indexes = collectTupledotIndexes(v, []);
	newInd = getTupledotIndexFromFlatTuple(indexes, fieldType, 0);
	WiTupledot(loweredValue, newInd, value.eclass);
}

lowerTuplesInWiWasmOp(v : WiWasmOp, unionFindMap : UnionFindMap<[WiseAstType]>, tmpCntr : ref int, eclass : int, onError : (WiseAst, string) -> void) -> WiExp {
	lowerTuplesInWiWarg = \v2 -> switch (v2 : WiWarg) {
		WiInt(__, __): v2;
		WiVar(__, __): v2;
		WiType() : lowerTuplesInWiType(v2);
	}
	rec = \v2 -> lowerTuplesInWiExp(v2, unionFindMap, tmpCntr, 0, onError);

	exps = map(v.exps, \e -> {
		fieldType = resolveWiseAstTypeByEclass(unionFindMap, e.eclass, onError);
		Pair(fieldType, isSameStructType(fieldType, WiseAstTypeTuple([])));
	});
	wargs = map(v.wargs, lowerTuplesInWiWarg);

	hasTuple = \-> exists(exps, secondOfPair);
	makeDefOp = \-> WiWasmOp(v.s, wargs, map(v.exps, rec), v.eclass);
	makeAdr = \adr, type -> WiAdd(adr, WiInt(calculateSizeOfType([], type), 0), adr.eclass);

	if (startsWith(v.s, "load") && v.exps != [] && eclass > 0) {
		fieldType = resolveWiseAstTypeByEclass(unionFindMap, eclass, onError);
		if (isSameStructType(fieldType, WiseAstTypeTuple([]))) {
			// tree tuple
			fieldTypes = WiseAstTypeTuple2WiTypes(fieldType, eclass, unionFindMap);
			WiTuple(
				fold2(fieldTypes, rec(v.exps[0]), [], \adr : WiExp, ops, valType : WiType -> Pair(
					makeAdr(adr, valType),
					arrayPush(ops, WiWasmOp(v.s, wargs, [adr], 0))
				)).second,
				0
			);
		} else {
			makeDefOp();
		}
	} else if (startsWith(v.s, "store") && length(v.exps) == 2 && exps[1].second) {
		fieldTypes = unrollWiTupletype(WiseAstTypeTuple2WiTypes(exps[1].first, v.exps[1].eclass, unionFindMap));
		if (fieldTypes == []) {
			onError(v, "ERROR : Can't find tuple fields in wasmOp.");
			WiBrace([], v.eclass);
		} else if (length(fieldTypes) == 1) {
			adr = rec(v.exps[0]);
			WiWasmOp(v.s, wargs, [adr, WiTupledot(v.exps[1], 0, fieldTypes[0].eclass)], 0);
		} else {
			tmpVar = makeTmpVarForTuple(tmpCntr);
			WiLet(
				tmpVar.id,
				0,
				WiTupletype(fieldTypes, 0),
				rec(v.exps[1]),
				WiBrace(
					foldi2(fieldTypes, rec(v.exps[0]), [], \i, adr : WiExp, ops, valType : WiType -> Pair(
						makeAdr(adr, valType),
						arrayPush(ops, WiWasmOp(v.s, wargs, [adr, WiTupledot(tmpVar, i, valType.eclass)], 0))
					)).second,
					v.eclass
				),
				0
			);
		}
	} else  if (hasTuple()) {
		onError(v, "ERROR : Can't apply wasmOp for tuple.");
		WiBrace([], v.eclass);
	} else {
		makeDefOp();
	}
}

extractWiTupledotValue(v : WiExp) -> WiExp {
	switch (v : WiExp) {
		WiTupledot(e, __, __): extractWiTupledotValue(e);
		default : v;
	}
}

collectTupledotIndexes(e : WiExp, acc : [int]) -> [int] {
	switch (e : WiExp) {
		WiTupledot(e1, ind, __) : collectTupledotIndexes(e1, concat([ind], acc));
		default : acc;
	}
}

getTupledotIndexFromFlatTuple(path : [int], type : WiseAstType, acc : int) -> int {
	if (path == []) {
		acc
	} else {
		switch (type : WiseAstType) {
			WiseAstTypeTuple(fields) : {
				ind = path[0];
				if (existsIndex(fields, ind)) {
					len = fold(subrange(fields, 0, ind), acc, \acc2, f -> acc2 + getFlatTupleLength(f, 0));
					getTupledotIndexFromFlatTuple(tail(path), fields[ind], len);
				} else {
					println("error : wrong tuple length. index=" + i2s(ind) + ". " + toString(type));
					acc;
				}
			}
			default : {
				println("error : expected tuple, got basic type. " + toString(type) + " : " + toString(path));
				acc;
			}
		}
	}
}

getFlatTupleLength(type : WiseAstType, acc : int) -> int {
	switch (type : WiseAstType) {
		WiseAstTypeTuple(fields) : fold(fields, acc, \acc2, f -> acc2 + getFlatTupleLength(f, 0));
		default : acc + 1;
	}
}

unrollWiTuple(v : WiTuple, unionFindMap : UnionFindMap<[WiseAstType]>, tmpCntr : ref int, onError : (WiseAst, string) -> void) -> WiExp {
	unpair(
		unrollWiTupleFields(v.exps, unionFindMap, tmpCntr, onError),
		\beforeExps, tupleFields -> {
			newTuple = WiTuple(tupleFields, 0);
			beforeExps ?? setLastEmptyBrace(beforeExps, newTuple) : newTuple;
		}
	);
}

unrollWiTupleFields(exps : [WiExp], unionFindMap : UnionFindMap<[WiseAstType]>, tmpCntr : ref int, onError : (WiseAst, string) -> void) -> Pair<Maybe<WiLet>, [WiExp]> {
	foldi2(exps, None(), [], \i, beforeExps, tupleFields : [WiExp], fe -> {
		fieldType = resolveWiseAstTypeByEclass(unionFindMap, fe.eclass, \inf, err -> println(err + " [" + toString(inf) + "]"));
		switch (fieldType : WiseAstType) {
			default : Pair(beforeExps, arrayPush(tupleFields, fe));
			WiseAstTypeTuple(__) : {
				tmpVar = makeTmpVarForTuple(tmpCntr);
				fieldTypes = unrollWiTupletype(WiseAstTypeTuple2WiTypes(fieldType, fe.eclass, unionFindMap));
				value = lowerTuplesInWiExp(fe, unionFindMap, tmpCntr, 0, onError);
				newLet = WiLet(tmpVar.id, 0, WiTupletype(fieldTypes, 0), value, WiBrace([], 0), 0);
				tmpTuple = Some(eitherMap(beforeExps, \b -> setLastEmptyBrace(b, newLet), newLet));
				newTupleFields = mapi(fieldTypes, \j, t -> WiTupledot(tmpVar, j, t.eclass));
				Pair(tmpTuple, concat(tupleFields, newTupleFields));
			}
		}
	});
}

makeTmpVarForTuple(tmpCntr : ref int) -> WiVar {
	tmpVar = WiVar("_tmp" + i2s(^(tmpCntr)), 0);
	tmpCntr := (^(tmpCntr)) + 1;
	tmpVar;
}

WiseAstTypeTuple2WiTypes(type : WiseAstType, eclass : int, unionFindMap : UnionFindMap<[WiseAstType]>) -> [WiType] {
	origTypes = wiseAstType2wiType(type, eclass, unionFindMap);
	types = switch (origTypes : WiType) {
		WiTupletype(t, __): t;
		default : {
			println("ERROR: wrong tuple type. Found " + toString(origTypes));
			[];
		}
	}
	types
}

setLastEmptyBrace(l : WiLet, v : WiExp) -> WiLet {
	if (l.brace.expsemi == []) {
		WiLet(l with brace = WiBrace([v], v.eclass));
	} else {
		lastVal = l.brace.expsemi[length(l.brace.expsemi) - 1];
		switch (lastVal : WiExp) {
			WiLet(__, __, __, __, __, __) : {
				newVal = setLastEmptyBrace(lastVal, v);
				WiLet(l with brace = WiBrace(arrayPush(subrange(l.brace.expsemi, 0, length(l.brace.expsemi) - 1), newVal), newVal.eclass))
			}
			default : l;
		}
		
	}
}

mapWiTupletype2wiExp(type : WiTupletype, acc : ?, mapType : (WiType, ?) -> Pair<WiExp, ?> ) -> Pair<WiExp, ?> {
	fields = fold2(type.types, [], acc, \exps, newAcc, fieldType -> {
		v = switch (fieldType : WiType) {
			WiBasictype(__, __) : mapType(fieldType, newAcc);
			WiFntype(__, __, __) : mapType(fieldType, newAcc);
			WiTupletype(__, __) : mapWiTupletype2wiExp(fieldType, newAcc, mapType);
		}
		unpair(v, \e, newAcc2 -> Pair(arrayPush(exps, e), newAcc2));
	});
	
	Pair(WiTuple(fields.first, type.eclass), fields.second);
}